plugins {
    id "java-library"
    id "org.openjfx.javafxplugin" version "0.0.9"
    id "maven-publish"
    id "signing"
}

group "bayern.steinbrecher"
version "0.1"

repositories {
    mavenCentral()
}

dependencies {
    implementation "bayern.steinbrecher:Wizard:1.20"
    implementation "org.jetbrains:annotations:19.0.0"
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

plugins.withType(JavaPlugin).configureEach {
    java {
        modularity.inferModulePath = true
        withSourcesJar()
        withJavadocJar()
    }
}

javafx {
    version = "14"
    modules = ["javafx.controls", "javafx.fxml", "javafx.graphics"]
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${ossrhUsername}"
                password = "${ossrhPassword}"
            }
        }
    }
    publications {
        mavenScreenSwitcher(MavenPublication) {
            pom {
                name = 'ScreenSwitcher'
                description = 'An abstract structure for JavaFX based applications for handling dialogs and switching between them.'
                url = 'https://github.com/TrackerSB/ScreenSwitcher'
                licenses {
                    license {
                        name = 'The Beer-Ware License (Revision 42)'
                    }
                }
                developers {
                    developer {
                        id = 'stefanHuber'
                        name = 'Stefan Huber'
                        email = 'stefan.huber.niedling@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TrackerSB/ScreenSwitcher.git'
                    developerConnection = 'scm:git:ssh://github.com/TrackerSB/ScreenSwitcher.git'
                    url = 'https://steinbrecher.bayern'
                }
            }
            from components.java
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenScreenSwitcher
}
